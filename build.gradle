plugins {
    id 'java'
    id "application"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'edu.sc.seis.launch4j' version '2.5.0'
}
group 'com.seedfinding.neil'

defaultTasks 'clean', 'release', 'run'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {url "https://jitpack.io"}
}

dependencies {
    implementation('com.github.KaptainWutax:MathUtils:5531c4a87b0f1bb85d1dab2bdd18ce375400626a') { transitive = false }
    implementation('com.github.KaptainWutax:SeedUtils:b6a383113ce5d8d09a59e91b28ff064fb97c0709') { transitive = false }
    implementation('com.github.KaptainWutax:MCUtils:424f9dd6ed25672c314ab8fd2101f9634f0e2707') { transitive = false }
    implementation('com.github.KaptainWutax:NoiseUtils:a3954932b15a889aa51c82176942d62f291c3b12') { transitive = false }

    implementation('com.github.KaptainWutax:BiomeUtils:0b7e2bd4f011ea8e1191c78df05e91bac052b568') { transitive = false }
    implementation('com.github.KaptainWutax:TerrainUtils:21b6d72b7798403abf620f736d0f1972daf822ed') { transitive = false }
    implementation('com.github.KaptainWutax:FeatureUtils:e4f6db824ef763c3772abd8e07ee9e2c30e2f2d3') { transitive = false }

    implementation('com.github.KaptainWutax:ChunkRandomReversal:6b76fb5cf2cd438de56e6a46cea2a83985831834') { transitive = false }

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.4.2')
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

application {
    mainClass.set("com.seedfinding.neil.Main")
}

test {
    useJUnitPlatform {
        includeEngines("junit-jupiter")
        maxHeapSize = "1G"
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
        showStandardStreams = true
        showExceptions true
        showCauses true
        showStackTraces true
    }
}


shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"
}

launch4j {
    outfile = "${project.name}-${project.version}.exe"
    outputDir = "libs"
    supportUrl = "https://github.com/hube12"
    jvmOptions = ["-Xms256M", "-Xmx2G"]
    initialHeapPercent = 5
    maxHeapPercent = 80
    jarTask = project.tasks.shadowJar
    productName = "${project.name}"
    fileDescription = "A fast enough nether ocean cracker"
    copyConfigurable = [] // don't copy lib
    libraryDir= "ignore_libs"
    jreMaxVersion=JavaVersion.VERSION_HIGHER
    jreMinVersion=JavaVersion.VERSION_1_8
    // this is a dirty fix to use JAVA_HOME
    bundledJrePath ="%JAVA_HOME%;%PATH%"
    dontWrapJar=false
    headerType="console"
}

task("release"){
    dependsOn(shadowJar)
    dependsOn(createExe)
}
